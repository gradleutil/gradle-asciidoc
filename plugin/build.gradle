plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.11.0"
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

version = "1.1.0.2"
group = 'net.gradleutil'

final adocVer = '3.3.2'

dependencies {
    api 'org.asciidoctor:asciidoctor-gradle-base:' + adocVer
    api 'org.asciidoctor:asciidoctor-gradle-jvm:' + adocVer
    api 'org.asciidoctor:asciidoctor-gradle-jvm-pdf:' + adocVer
    // Use the awesome Spock testing and specification framework
    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
}

// Add a source set for the functional test suite
sourceSets {
    functionalTest {
    }
}

configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

// Add a task to run the functional tests
tasks.register('functionalTest', Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    useJUnitPlatform()
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.named('check') {
    // Run the functional tests as part of `check`
    dependsOn(tasks.functionalTest)
}

tasks.named('test') {
    // Use JUnit Jupiter for unit tests.
    useJUnitPlatform()
}

gradlePlugin {
    // Define the plugin
    plugins {
        gradleUtilAsciidoc {
            id = 'net.gradleutil.gradle-asciidoc'
            implementationClass = 'net.gradleutil.asciidoc.GradleUtilAsciidocPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/gradleutil/gradle-asciidoc'
    vcsUrl = 'https://github.com/gradleutil/gradle-asciidoc'
    description = 'Asciidoc gradle util plugin for docs that include gradle builds/tasks'
    tags = ['build-tool', 'documentation', 'asciidoc', 'gradle-util']

    plugins {
        gradleUtilAsciidoc {
            id = 'net.gradleutil.gradle-asciidoc'
            displayName = 'Asciidoc gradle util plugin for docs that include gradle builds/tasks'
        }
    }
}

tasks.withType(AbstractPublishToMaven) { publishTask ->
    def printInfo = { AbstractPublishToMaven task, String repoPath ->
        task.publication.with { p ->
            def sb = new StringBuilder()
            p.artifacts.each {
                String artifactPath = p.groupId.replace(".", "/") + "/" + p.artifactId + "/" + p.version + "/"
                sb.append('\n  ').append(repoPath).append(artifactPath)
                        .append(p.artifactId).append('-').append(p.version)
                        .append(it.classifier ? '-' + it.classifier : '').append('.').append(it.extension)
            }
            logger.lifecycle("Published ${p.groupId}:${p.artifactId}:${p.version}${sb.toString()}")
        }
    }
    doLast {
        if (publishTask instanceof PublishToMavenRepository) {
            printInfo(publishTask, publishTask.repository.url.toString())
        } else {
            String repoPath = repositories.mavenLocal().url.toURL().getFile()
            printInfo(publishTask, repoPath)
        }
    }
}
